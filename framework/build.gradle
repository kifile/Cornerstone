apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'signing'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.1"

    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 23
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
}

signing {
    sign configurations.archives
}

group = 'com.kifile.android.cornerstone'
archivesBaseName = 'Cornerstone'
version = '1.0.1'

uploadArchives {
    repositories {
        mavenDeployer {
            beforeDeployment {
                MavenDeployment deployment -> signing.signPom(deployment);
            }

            repository(url: "https://oss.sonatype.org/service/local/staging/deploy/maven2/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            snapshotRepository(url: "https://oss.sonatype.org/content/repositories/snapshots/") {
                authentication(userName: ossrhUsername, password: ossrhPassword)
            }

            pom.project {
                name 'Cornerstone'
                packaging 'aar'
                description 'A data manager framework for Android.'
                url 'https://github.com/kifile/Cornerstone'

                scm {
                    connection 'scm:git:https://github.com/kifile/Cornerstone.git'
                    developerConnection 'scm:git:https://github.com/kifile/Cornerstone.git'
                    url 'https://github.com/kifile/Cornerstone.git'
                }

                licenses {
                    license {
                        name 'The Apache License, Version 2.0'
                        url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }

                developers {
                    developer {
                        id 'kifile'
                        name 'Kifile Chou'
                        email 'kifile@kifile.com'
                    }
                }
            }
        }
    }
}
